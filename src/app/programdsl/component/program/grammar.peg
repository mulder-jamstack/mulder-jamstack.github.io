Root := rooms=Rooms papers=Papers events=Events program=Program

Rooms := '#' _ '[Rr]ooms' ENDLINE* rooms=Room*
Room := _ '-' _ name=STRING _ capacite={',' _ value=INT}? ENDLINE*

Papers := '#' _ '[Pp]apers' _ ENDLINE* papers=Paper*
Paper := '##' _ paperName=STRING ENDLINE* authors=Person* abstract=Abstract? video=Video?
Person := _ '-' _ name=STRING homepage={_ ',' _ value=LINK _}? email={_ ',' _ value=EMAIL _}? about={_ ',' _ value=STRING _}? ENDLINE*

Abstract := _ '\*abstract\*:' ENDLINE text=MULTISTRING ENDLINE*
Video := _ '\*video\*:' _ url=LINK ENDLINE*

Events := '#' _ '[Ee]vents' _ ENDLINE* events=Event*
Event := SimpleEvent | OrganizedEvent | TalkSession
SimpleEvent := '##' _ eventName=STRING ENDLINE* abstract=Abstract? ENDLINE*
OrganizedEvent := '##' _ '\(' _ eventType=STRING _ '\)' _ eventName=STRING ENDLINE* organizers=Person* abstract=Abstract? ENDLINE*
TalkSession := '##' _ '\[' _ eventType=STRING _ '\]' _ eventName=STRING ENDLINE* organizers=Person* abstract=Abstract? ENDLINE* '###' _ '[Pp]apers' _ ENDLINE* papers={ _ '-' _ name=STRING ENDLINE*}*

Program := '#' _ '[Pp]rogram' _ ENDLINE* days=Day*
Day := _ '##' _ date=DATE _ ENDLINE* eventinfo=EventInfo*
EventInfo := _ '-' _ timeSlot={start=HOUR _ '-' _ end=HOUR} _ 'in' _ roomName=STRING _ ':' _ eventName=STRING _ ENDLINE*


EMAIL := '[a-zA-Z0-9.!#$%&\'*+/=?^_`{|}~-]+@[a-zA-Z0-9-]+(?:\.[a-zA-Z0-9-]+)*'
LINK := 'https?:\/\/(www\.)?[-a-zA-Z0-9@:%._\+~#=]{1,256}\.[a-zA-Z0-9()]{1,6}\b([-a-zA-Z0-9()@:%_\+.~#?&//=]*)'

DATE := '(19|20)[0-9][0-9]-(0[1-9]|1[012])-(0[1-9]|[12][0-9]|3[01])'
HOUR := '(0[0-9]|1[0-9]|2[0-3]):([0-5][0-9])'

STRING := '[a-zA-Z0-9]([a-zA-Z0-9.\- ]*[a-zA-Z0-9.])?'
MULTISTRING := '([^\n\r]+\r?\n)*'
INT := '[0-9]+'
ENDLINE := _ ';|\n'
_ := '[ \t\r\f]*'